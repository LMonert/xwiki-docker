/*
 * See the NOTICE file distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This is free software; you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation; either version 2.1 of
 * the License, or (at your option) any later version.
 *
 * This software is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
import org.apache.tools.ant.filters.ReplaceTokens

// Run this Gradle build with 'gradle' to generate the various versions and variants from the template directory.
// Whenever a new version of XWiki is out, update this file to update the token values, run gradle and commit the
// result.
//
// Note: As a consequence only update the template files and never the generated files!

defaultTasks 'generate'

def variants = ['mysql-tomcat', 'mariadb-tomcat', 'postgres-tomcat']

// Notes:
// - To compute the XWiki sha256, download the XWiki WAR from
//   http://nexus.xwiki.org/nexus/content/groups/public/org/xwiki/platform/xwiki-platform-distribution-war, and issue:
//   - Unix: sha256sum <binary name>
//   - Mac: shasum --algorithm 256 <binary name>
// - To compute the mysql JDBC sha256, get the JAR at https://search.maven.org/artifact/com.mysql/mysql-connector-j and
//   do the same as for the XWiki sha256.
// - To compute the MariaDB JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.mariadb.jdbc/mariadb-java-client and  do the same as for the XWiki sha256.
// - To compute the Postgres JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.postgresql/postgresql and do the same as for the XWiki sha256.
// - Use the JDBC driver versions found in the XWiki POM for the specified versions:
//   - mysql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L110 (link for master)
//   - mariadb: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L113 (link for master)
//   - postgresql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L119 (link for master)

def tokens = [
    '16': [
        xwikiVersion: '16.6.0',
        xwikiSha256: '504e3fc3707d222d4a2be7c4e51e01242d4a7092380234d5076616d170390fe7',
        mysqlJDBCVersion: '9.0.0',
        mysqlJDBCSha256: 'a221c4106b7fe68a45912cdbf8351f1b43ad3c53a43c3bc966181cc14f86fa30',
        mariadbJDBCVersion: '3.4.1',
        mariadbJDBCSha256: 'f60e4b282f1f4bdb74f0a26436ba7078a5e480b6f6702f6a7b45d9ba5e604a24',
        postgresJDBCVersion: '42.7.3',
        postgresJDBCSha256: 'a2644cbfba1baa145ff7e8c8ef582a6eed7a7ec4ca792f7f054122bdec756268'
    ],
    '16.4': [
        xwikiVersion: '16.4.2',
        xwikiSha256: '59a00c0f3a681f5931575a6fa13799a6e1cdf16a31e5a9075a698662b9f414b7',
        mysqlJDBCVersion: '8.4.0',
        mysqlJDBCSha256: 'd77962877d010777cff997015da90ee689f0f4bb76848340e1488f2b83332af5',
        mariadbJDBCVersion: '3.4.1',
        mariadbJDBCSha256: 'f60e4b282f1f4bdb74f0a26436ba7078a5e480b6f6702f6a7b45d9ba5e604a24',
        postgresJDBCVersion: '42.7.3',
        postgresJDBCSha256: 'a2644cbfba1baa145ff7e8c8ef582a6eed7a7ec4ca792f7f054122bdec756268'
    ],
    '15': [
        xwikiVersion: '15.10.11',
        xwikiSha256: 'b69de0d6ae0d2cdd10efcd1913065f750de62b5147f553bc6772e42cc66e2e2c',
        mysqlJDBCVersion: '8.4.0',
        mysqlJDBCSha256: 'd77962877d010777cff997015da90ee689f0f4bb76848340e1488f2b83332af5',
        mariadbJDBCVersion: '3.4.1',
        mariadbJDBCSha256: 'f60e4b282f1f4bdb74f0a26436ba7078a5e480b6f6702f6a7b45d9ba5e604a24',
        postgresJDBCVersion: '42.7.3',
        postgresJDBCSha256: 'a2644cbfba1baa145ff7e8c8ef582a6eed7a7ec4ca792f7f054122bdec756268'
    ],
    '14': [
        xwikiVersion: '14.10.21',
        xwikiSha256: '72a634e2aeb085878dce2629a3e5e6136887d0c22712dcee5a284be8143135ea',
        mysqlJDBCVersion: '8.4.0',
        mysqlJDBCSha256: 'd77962877d010777cff997015da90ee689f0f4bb76848340e1488f2b83332af5',
        mariadbJDBCVersion: '3.4.1',
        mariadbJDBCSha256: 'f60e4b282f1f4bdb74f0a26436ba7078a5e480b6f6702f6a7b45d9ba5e604a24',
        postgresJDBCVersion: '42.7.3',
        postgresJDBCSha256: 'a2644cbfba1baa145ff7e8c8ef582a6eed7a7ec4ca792f7f054122bdec756268'
    ]
]

task generate() {
    doLast {
        // Copy the template for all versions and variants
        tokens.keySet().each() { version ->
            variants.each() { variant ->
                // Extract the db type and add it as a token
                def (db, servlet) = variant.tokenize('-')
                tokens[version].'db' = db
                // Copy common template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include '.env'
                    include 'Dockerfile'
                    include 'docker-compose.yml'
                    include 'xwiki/*'
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy DB-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${db}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy Servlet-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${servlet}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
            }
        }
    }
}
